[ ТЗ: написать программу, осуществляющую слияние двух отсортированных массивов ]
[ Длины массивов записываются в ячейки 2, 3, 4; их адреса - в ячейки 5, 6, 7  ]
[ Ячейки 0, 1 и 8 - рабочие ]
[ Подпрограмма находится в ячейках 56 - 113 (56 + 57) ]
[ Тогда 0 элемент выходного массива находится в точке (начало основной программы) 114 + 33 = 147 ]

	[sub:]
		T 56 K [ директива IO2, установка адреса загрузки ]

		G K [ директива IO2, фиксация начального адреса программы ]
[0] 		A 3 F [ пролог: формирование кода инструкции возврата в Acc ]
[1] 		T 56 [<ret>] @ [ пролог: запись инструкции возврата ]

[2] 		A 5 [<addr>] F [ загрузка в аккумулятор адреса 0-го элемента выходного массива ]
[3] 		A 49 [<w>] @ [ прибавляем код инструкции с нулевым полем адреса ]
[4] 		T 49 [<w>] @ [ запись сформированной инструкции, обнуление аккумулятора ]

[5] 		A 6 [<addr1>] F [ загрузка в аккумулятор адреса 0-го элемента массива 1 ]
[6] 		A 15 [<r1>] @ [ прибавляем код инструкции с нулевым полем адреса ]
[7] 		T 15 [<r1>] @ [ запись сформированной инструкции, обнуление аккумулятора ]

[8] 		A 7 [<addr2>] F [ загрузка в аккумулятор адреса 0-го элемента массива 2 ]
[9] 		A 17 [<r2>] @ [ прибавляем код инструкции с нулевым полем адреса ]
[10] 		T 17 [<r2>] @ [ запись сформированной инструкции, обнуление аккумулятора ]

	[loop:]
[11] 		A 2 F [ загружаем счетчик необработанных элементов массивов ]
[12] 		S 57 [<c1>] @ [ уменьшаем на 1 ]
[13] 		G 55 [<exit>] @ [ если результат меньше 0 завершаем работу ]
[14] 		T 2 F [ обновляем значение счетчика и обнуляем аккумулятор ]

	[r1:]
[15] 		A 0 F [ загрузка в аккумулятор значения из ячейки N первого массива ]
[16] 		T 0 F [ запись этого значения в рабочую ячейку 0, обнуление аккумулятора ]
	[r2:]
[17] 		A 0 F [ загрузка в аккумулятор значения из ячейки K второго массива ]
[18] 		T 1 F [ запись этого значения в рабочую ячейку 1, обнуление аккумулятора ]

[19] 		S 57 [<c1>] @ [ уменьшаем на 1 ]

[20] 		A 3 F [ загружаем счетчик необработанных элементов массива 1] 
[21] 		G 38 [<branch2>] @ [ если результат меньше 0 добавляем элемент 2 массива ]
[22] 		S 3 F [ восстанавливаем -1 ]

[23] 		A 4 F [ загружаем счетчик необработанных элементов массива 2 ]
[24] 		G 29 [<branch1>] @ [ если результат меньше 0 добавляем элемент 1 массива ]
[25] 		T 8 F [ обнуление аккумулятора ]

[26] 		A 1 F [ загружаем элемент второго массива ]
[27] 		S 0 F [ вычитаем из элемента второго массива элемент первого массива ]
[28] 		G 38 [<branch2>] @ [ элемент второго массива меньше - добавляем его ]

	[branch1:]
[29] 		T 8 F [ обнуление аккумулятора ]

[30] 		A 3 F [ загружаем счетчик необработанных элементов массива 1] 
[31] 		S 57 [<c1>] @ [ уменьшаем на 1 ]
[32] 		T 3 F [ обновляем значение счетчика и обнуляем аккумулятор ]

[33] 		A 57 [<c1>] @ [ загрузка в аккумулятор константы 1 ]
[34] 		L 0 D [ сдвиг аккумулятора на 1 разряд влево ]
[35] 		A 15 [<r1>] @ [ прибавляем код инструкции с нулевым полем адреса ]
[36] 		T 15 [<r1>] @ [ запись сформированной инструкции, обнуление аккумулятора ]

[37] 		E 48 [<finally>] @ [ не добавляем элемент 2 массива ]

	[branch2:]
[38] 		T 8 F [ обнуление аккумулятора ]

[39] 		A 1 F [ считываем элемент 2 массива ]
[40] 		T 0 F [ записываем элемент 2 массива в ячейку 0 ]

[41] 		A 4 F [ загружаем счетчик необработанных элементов массива 2] 
[42] 		S 57 [<c1>] @ [ уменьшаем на 1 ]
[43] 		T 4 F [ обновляем значение счетчика и обнуляем аккумулятор ]

[44] 		A 57 [<c1>] @ [ загрузка в аккумулятор константы 1 ]
[45] 		L 0 D [ сдвиг аккумулятора на 1 разряд влево ]
[46] 		A 17 [<r2>] @ [ прибавляем код инструкции с нулевым полем адреса ]
[47] 		T 17 [<r2>] @ [ запись сформированной инструкции, обнуление аккумулятора ]

	[finally:]
[48] 		A 0 F [ загрузка в аккумулятор значения меньшего элемента ]
	[w:]
[49] 		T 0 F [ запись этого значения в ячейку с адресом M, обнуление аккумулятора ]

[50] 		A 57 [<c1>] @ [ загрузка в аккумулятор константы 1 ]
[51] 		L 0 D [ сдвиг аккумулятора на 1 разряд влево ]
[52] 		A 49 [<w>] @ [ прибавляем код инструкции с нулевым полем адреса ]
[53] 		T 49 [<w>] @ [ запись сформированной инструкции, обнуление аккумулятора ]

[54] 		E 11 [<loop>] @ [ повторяем все операции ]

	[exit:]
[55] 		T 0 F [ обнуление аккумулятора ]

	[ret:]
[56] 		E 0 F [ эпилог: инструкция возврата из подпрограммы ]

	[c1:]
[57] 		P 0 D [ константа 1 ]



	[:main]
		G K [ директива IO2, фиксация начального адреса программы ]

[0] 		X 0 F [ для пошаговой отладки использовать Z 0 F ]

[1] 		A 16 [<len>] @ [ длина выходного массива ]
[2] 		T 2 F [ запись адреса массива в ячейку 2, обнуление аккумулятора ]
[3] 		A 17 [<len1>] @ [ длина массива 1 ]
[4] 		T 3 F [ запись адреса массива в ячейку 3, обнуление аккумулятора ]
[5] 		A 18 [<len2>] @ [ длина массива 2 ]
[6] 		T 4 F [ запись адреса массива в ячейку 4, обнуление аккумулятора ]

[7] 		A 19 [<addr>] @ [ адрес выходного массива ]
[8] 		T 5 F [ запись адреса выходного массива в ячейку 5, обнуление аккумулятора ]
[9] 		A 20 [<addr1>] @ [ адрес массива 1]
[10] 		T 6 F [ запись адреса массива 1 в ячейку 6, обнуление аккумулятора ]
[11]		 A 21 [<addr2>] @ [ адрес массива 2]
[12] 		T 7 F [ запись адреса массива 2 в ячейку 7, обнуление аккумулятора ]

[13] 		A 13 @          [\ вызов ]
[14] 		G 56 F [<sub>]  [/ подпрограммы ]
[15] 		Z 0 F [ останов ]

	[len:]
[16] 		P 5 D [ 11 ]
	[len1:]
[17] 		P 2 F [ 4 ]
	[len2:]
[18] 		P 3 D [ 7 ]

	[addr:]
[19] 		P 33 [<array>] @ [ адрес выходного массива = <Начало программы> + 33 ]
	[addr1:]
[20] 		P 22 [<array1>] @ [ адрес массива 1 = <Начало программы> + 22 ]
	[addr2:]
[21] 		P 26 [<array2>] @ [ адрес массива 2 = <Начало программы> + 26 ]

	[array1:]
[22] 		P 1 F [ 2 ]
[23] 		P 2 D [ 5 ]
[24] 		P 3 F [ 6 ]
[25] 		P 4 F [ 8 ]
	[array2:]
[26] 		P 0 F [ 0 ]
[27] 		P 0 D [ 1 ]
[28] 		P 1 D [ 3 ]
[29] 		P 2 F [ 4 ]
[30] 		P 3 D [ 7 ]
[31] 		P 4 D [ 9 ]
[32] 		P 5 F [ 10 ]
	[array]
[33]

		EZ PF [ директива IO2, переход к исполнению ]
