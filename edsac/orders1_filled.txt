[ ТЗ: написать программу, осуществляющую слияние двух отсортированных массивов ]
[ Длина выходного массива хранится в 98, 1 массива в 99, 2 массива в 100; ]
[ Адреса хранятся соответственно в 101, 102, 103 ]
[ Массив 1 хранится в 104 - 107, массив 2 в 108 - 114 ]
[ Ячейки 0 - 5 рабочие ]
[ Результат находится в 115 - 125 ]

		T 115 S

[32] 		X 0 S [ для отладки ]
[33] 		T 0 S [ запись аккумулятора в рабочую ячейку 0, обнуление аккумулятора ]

[34] 		A 98 [<len>] S [ загрузка в аккумулятор длины выходного массива ]
[35] 		T 2 S [ запись этого значения в ячейку 2 ]

[36] 		A 99 [<len1>] S [ загрузка в аккумулятор длины 1 обрабатываемого массива ]
[37] 		T 3 S [ запись этого значения в ячейку 3 ]

[38] 		A 100 [<len2>] S [ загрузка в аккумулятор длины 2 обрабатываемого массива ]
[39] 		T 4 S [ запись этого значения в ячейку 4 ]

[40] 		A 101 [<addr>] S [ загрузка в аккумулятор адреса 0-го элемента выходного массива ]
[41] 		L 0 L [ сдвиг аккумулятора на 1 разряд влево ]
[42] 		A 90 [<w>] S [ прибавляем код инструкции с нулевым полем адреса ]
[43] 		T 90 [<w>] S [ запись сформированной инструкции, обнуление аккумулятора ]

[44] 		A 102 [<addr1>] S [ загрузка в аккумулятор адреса 0-го элемента массива 1 ]
[45] 		L 0 L [ сдвиг аккумулятора на 1 разряд влево ]
[46] 		A 56 [<r1>] S [ прибавляем код инструкции с нулевым полем адреса ]
[47] 		T 56 [<r1>] S [ запись сформированной инструкции, обнуление аккумулятора ]

[48] 		A 103 [<addr2>] S [ загрузка в аккумулятор адреса 0-го элемента массива 2 ]
[49] 		L 0 L [ сдвиг аккумулятора на 1 разряд влево ]
[50] 		A 58 [<r2>] S [ прибавляем код инструкции с нулевым полем адреса ]
[51] 		T 58 [<r2>] S [ запись сформированной инструкции, обнуление аккумулятора ]
 
	[loop:]
[52] 		A 2 S [ загружаем счетчик необработанных элементов массивов ]
[53] 		S 97 [<c1>] S [ уменьшаем на 1 ]
[54] 		G 96 [<exit>] S [ если результат меньше 0 завершаем работу ]
[55] 		T 2 S [ обновляем значение счетчика и обнуляем аккумулятор ]

	[r1:]
[56] 		A 0 S [ загрузка в аккумулятор значения из ячейки N первого массива ]
[57] 		T 0 S [ запись этого значения в рабочую ячейку 0, обнуление аккумулятора ]
	[r2:]
[58] 		A 0 S [ загрузка в аккумулятор значения из ячейки K второго массива ]
[59] 		T 1 S [ запись этого значения в рабочую ячейку 1, обнуление аккумулятора ]

[60] 		S 97 [<c1>] S [ уменьшаем на 1 ]

[61] 		A 3 S [ загружаем счетчик необработанных элементов массива 1] 
[62] 		G 79 [<branch2>] S [ если результат меньше 0 добавляем элемент 2 массива ]
[63] 		S 3 S [ восстанавливаем -1 ]

[64] 		A 4 S [ загружаем счетчик необработанных элементов массива 2 ]
[65] 		G 70 [<branch1>] S [ если результат меньше 0 добавляем элемент 1 массива ]
[66] 		T 5 S [ обнуление аккумулятора ]

[67] 		A 1 S [ загружаем элемент второго массива ]
[68] 		S 0 S [ вычитаем из элемента второго массива элемент первого массива ]
[69] 		G 79 [<branch2>] S [ элемент второго массива меньше - добавляем его ]

	[branch1:]
[70] 		T 5 S [ обнуление аккумулятора ]

[71] 		A 3 S [ загружаем счетчик необработанных элементов массива 1] 
[72] 		S 97 [<c1>] S [ уменьшаем на 1 ]
[73] 		T 3 S [ обновляем значение счетчика и обнуляем аккумулятор ]

[74] 		A 97 [<c1>] S [ загрузка в аккумулятор константы 1 ]
[75] 		L 0 L [ сдвиг аккумулятора на 1 разряд влево ]
[76] 		A 56 [<r1>] S [ прибавляем код инструкции с нулевым полем адреса ]
[77] 		T 56 [<r1>] S [ запись сформированной инструкции, обнуление аккумулятора ]

[78] 		E 89 [<finally>] S [ не добавляем элемент 2 массива ]

	[branch2:]
[79] 		T 5 S [ обнуление аккумулятора ]

[80] 		A 1 S [ считываем элемент 2 массива ]
[81] 		T 0 S [ записываем элемент 2 массива в ячейку 0 ]

[82] 		A 4 S [ загружаем счетчик необработанных элементов массива 1] 
[83] 		S 97 [<c1>] S [ уменьшаем на 1 ]
[84] 		T 4 S [ обновляем значение счетчика и обнуляем аккумулятор ]

[85] 		A 97 [<c1>] S [ загрузка в аккумулятор константы 1 ]
[86] 		L 0 L [ сдвиг аккумулятора на 1 разряд влево ]
[87] 		A 58 [<r2>] S [ прибавляем код инструкции с нулевым полем адреса ]
[88] 		T 58 [<r2>] S [ запись сформированной инструкции, обнуление аккумулятора ]

	[finally:]

[89] 		A 0 S [ загрузка в аккумулятор значения меньшего элемента ]
	[w:]
[90] 		T 0 S [ запись этого значения в ячейку с адресом M, обнуление аккумулятора ]

[91] 		A 97 [<c1>] S [ загрузка в аккумулятор константы 1 ]
[92] 		L 0 L [ сдвиг аккумулятора на 1 разряд влево ]
[93] 		A 90 [<w>] S [ прибавляем код инструкции с нулевым полем адреса ]
[94] 		T 90 [<w>] S [ запись сформированной инструкции, обнуление аккумулятора ]

[95] 		E 52 [<loop>] S [ повторяем все операции ]

	[exit:]
[96] 		Z 0 S [ останов ]

	[c1:]
[97] 		P 0 L [ константа 1 ]

	[len]
[98] 		P 5 L [ 11 ]
	[len1:]
[99] 		P 2 S [ 4 ]
	[len2:]
[100] 		P 3 L [ 7 ]

	[addr:]
[101] 		P 57 L [115]
	[addr1:]
[102] 		P 52 S [104]
	[addr2:]
[103] 		P 54 S [108]

	[array1:]
[104] 		P 1 S [ 2 ]
[105] 		P 2 L [ 5 ]
[106] 		P 3 S [ 6 ]
[107] 		P 4 S [ 8 ]
	[array2:]
[108] 		P 0 S [ 0 ]
[109] 		P 0 L [ 1 ]
[110] 		P 1 L [ 3 ]
[111] 		P 2 S [ 4 ]
[112] 		P 3 L [ 7 ]
[113] 		P 4 L [ 9 ]
[114] 		P 5 S [ 10 ]
[115]
