#join.s
.text
join:
.globl join
  # в a0 <указатель выходного массива>
  # в a1 <указатель массива 1>  
  # в a2 <указатель массива 2>
  # в a3 <длина массива 1>
  # в a4 <длина массива 2>
  li a5, 0               # <счетчик обработанных элементов массива 1>
  li a6, 0               # <счетчик обработанных элементов массива 2>
  jal zero, loop_check
loop:
  lw t0, 0(a1)           # <элемент массива 1>
  lw t1, 0(a2)           # <элемент массива 2>
  bgeu a5, a3, branch2   # <все элементы массива 1 обработаны>
  bgeu a6, a4, branch1   # <все элементы массива 2 обработаны>
  bgeu t0, t1, branch2   # <элемент массива 1 больше или равен элемента массива 2>
branch1:
  sw t0, 0(a0)           # <записываем в выходной массив элемент массива 1>
  addi a5, a5, 1         # <увеличиваем счетчик обработанных элементов массива 1>
  addi a1, a1, 4         # <увеличиваем указатель массива 1>
  jal zero, finally
branch2:
  sw t1, 0(a0)           # <записываем в выходной массив элемент массива 2>
  addi a6, a6, 1         # <увеличиваем счетчик обработанных элементов массива 2>
  addi a2, a2, 4         # <увеличиваем указатель массива 2>
finally:
  addi a0, a0, 4         # <увеличиваем указатель выходного массива>
loop_check:
  bltu a5, a3, loop      # <есть необработанные элементы массива 1>
  bltu a6, a4, loop      # <есть необработанные элементы массива 2>
loop_exit:
  ret
